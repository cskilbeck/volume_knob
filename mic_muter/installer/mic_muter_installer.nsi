######################################################################
; Script generated by the HM NIS Edit Script Wizard.

######################################################################

!include WinMessages.nsh
!include "LogicLib.nsh"
!include "NSProcess.nsh"
 
######################################################################

!define PRODUCT_NAME "MicMuter"
!define PRODUCT_VERSION "v1.0"
!define PRODUCT_PUBLISHER "Tiny Little Things"
!define PRODUCT_WEB_SITE "http://www.skilbeck.com"
!define PLATFORM "Win32"

!define APP_EXE "MicMuter.exe"

!define PRODUCT_DIR_REGKEY "Software\Microsoft\Windows\CurrentVersion\App Paths\${APP_EXE}"
!define PRODUCT_UNINST_KEY "Software\Microsoft\Windows\CurrentVersion\Uninstall\${PRODUCT_NAME}"
!define PRODUCT_UNINST_ROOT_KEY "HKCU"

!define /math WM_APP_QUIT_PLEASE ${WM_USER} + 16

######################################################################
; MUI 1.67 compatible ------

!include "MUI.nsh"

######################################################################
; MUI Settings

!define MUI_ABORTWARNING
!define MUI_ICON "..\mic_muter\mic_muter.ico"
!define MUI_UNICON "${NSISDIR}\Contrib\Graphics\Icons\modern-uninstall.ico"

######################################################################
; Directory page
!insertmacro MUI_PAGE_DIRECTORY

######################################################################
; Instfiles page

!insertmacro MUI_PAGE_INSTFILES

######################################################################
; Finish page

!define MUI_FINISHPAGE_RUN "$INSTDIR\${APP_EXE}"
!insertmacro MUI_PAGE_FINISH

######################################################################
; Uninstaller pages

!insertmacro MUI_UNPAGE_INSTFILES

######################################################################
; Language files

!insertmacro MUI_LANGUAGE "English"

######################################################################
; MUI end ------

RequestExecutionLevel user
Name "${PRODUCT_NAME} ${PRODUCT_VERSION}"
OutFile "output\${PRODUCT_NAME}_${PLATFORM}_${PRODUCT_VERSION}.exe"
InstallDir "$LOCALAPPDATA\MicMuter"
InstallDirRegKey HKCU "${PRODUCT_DIR_REGKEY}" ""
ShowInstDetails show
ShowUnInstDetails show

######################################################################

LangString termMsg ${LANG_ENGLISH} "Installer cannot stop running ${PRODUCT_NAME}.$\nDo you want to terminate process?"
LangString stopMsg ${LANG_ENGLISH} "Stopping ${PRODUCT_NAME} Application"

!define WND_CLASS "mic_muter_overlay_FC29A1DC-16DE-4E9A-83E5-2DD9A5E034AA"
!define SYNC_TERM 0x00100001
!define TO_MS 2000

!macro TerminateApp
 
    Push $0 ; window handle
    Push $1
    Push $2 ; process handle
    DetailPrint "$(stopMsg)"
    FindWindow $0 '${WND_CLASS}' ''
    IntCmp $0 0 done
    System::Call 'user32.dll::GetWindowThreadProcessId(i r0, *i .r1) i .r2'
    System::Call 'kernel32.dll::OpenProcess(i ${SYNC_TERM}, i 0, i r1) i .r2'
    SendMessage $0 ${WM_CLOSE} 0 0 /TIMEOUT=${TO_MS}
    System::Call 'kernel32.dll::WaitForSingleObject(i r2, i ${TO_MS}) i .r1'
    IntCmp $1 0 close
    MessageBox MB_YESNO|MB_ICONEXCLAMATION "$(termMsg)" /SD IDYES IDYES terminate IDNO close
    goto close
  terminate:
    System::Call 'kernel32.dll::TerminateProcess(i r2, i 0) i .r1'
  close:
    System::Call 'kernel32.dll::CloseHandle(i r2) i .r1'
  done:
    Pop $2
    Pop $1
    Pop $0
 
!macroend
 
######################################################################

Section "MicMuter" SEC01
  !insertmacro TerminateApp
  SetOutPath "$INSTDIR"
  SetOverwrite ifnewer
  File "..\bin\${PLATFORM}\Release\${APP_EXE}"
  File "..\bin\${PLATFORM}\Release\mic_muter_dll.dll"
  CreateDirectory "$SMPROGRAMS\MicMuter"
  CreateShortCut "$SMPROGRAMS\MicMuter\MicMuter.lnk" "$INSTDIR\${APP_EXE}"
SectionEnd

######################################################################

Section -AdditionalIcons
  CreateShortCut "$SMPROGRAMS\MicMuter\Uninstall.lnk" "$INSTDIR\uninst.exe"
SectionEnd

######################################################################

Section -Post
  WriteUninstaller "$INSTDIR\uninst.exe"
  WriteRegStr HKCU "${PRODUCT_DIR_REGKEY}" "" "$INSTDIR\${APP_EXE}"
  WriteRegStr ${PRODUCT_UNINST_ROOT_KEY} "${PRODUCT_UNINST_KEY}" "DisplayName" "$(^Name)"
  WriteRegStr ${PRODUCT_UNINST_ROOT_KEY} "${PRODUCT_UNINST_KEY}" "UninstallString" "$INSTDIR\uninst.exe"
  WriteRegStr ${PRODUCT_UNINST_ROOT_KEY} "${PRODUCT_UNINST_KEY}" "DisplayIcon" "$INSTDIR\${APP_EXE}"
  WriteRegStr ${PRODUCT_UNINST_ROOT_KEY} "${PRODUCT_UNINST_KEY}" "DisplayVersion" "${PRODUCT_VERSION}"
  WriteRegStr ${PRODUCT_UNINST_ROOT_KEY} "${PRODUCT_UNINST_KEY}" "URLInfoAbout" "${PRODUCT_WEB_SITE}"
  WriteRegStr ${PRODUCT_UNINST_ROOT_KEY} "${PRODUCT_UNINST_KEY}" "Publisher" "${PRODUCT_PUBLISHER}"
SectionEnd

######################################################################

Function un.onUninstSuccess
  HideWindow
  MessageBox MB_ICONINFORMATION|MB_OK "$(^Name) was successfully removed from your computer."
FunctionEnd

######################################################################

Function un.onInit
  MessageBox MB_ICONQUESTION|MB_YESNO|MB_DEFBUTTON2 "Are you sure you want to completely remove $(^Name) and all of its components?" IDYES +2
  Abort
FunctionEnd

######################################################################

Section Uninstall

  !insertmacro TerminateApp

  Delete "$INSTDIR\uninst.exe"
  Delete "$INSTDIR\mic_muter_dll.dll"
  Delete "$INSTDIR\${APP_EXE}"

  Delete "$SMPROGRAMS\MicMuter\Uninstall.lnk"
  Delete "$SMPROGRAMS\MicMuter\MicMuter.lnk"

  RMDir "$SMPROGRAMS\MicMuter"
  RMDir "$INSTDIR"

  DeleteRegKey ${PRODUCT_UNINST_ROOT_KEY} "${PRODUCT_UNINST_KEY}"
  DeleteRegKey HKCU "${PRODUCT_DIR_REGKEY}"
  DeleteRegKey HKCU "SOFTWARE\${PRODUCT_NAME}"
  DeleteRegValue HKCU "SOFTWARE\Microsoft\Windows\CurrentVersion\Run" "${PRODUCT_NAME}"
  SetAutoClose true
SectionEnd

